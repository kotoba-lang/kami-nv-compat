(ns kotoba.lang.kami-nv-compat.wadachi-sim.world-test
  "wadachi-sim.world: oriented-box tessellation + ground-truth coverage."
  (:require [clojure.test :refer [deftest is]]
            [kotoba.lang.kami-nv-compat.wadachi-sim.world :as world]))

(deftest box-tris-shape
  (let [tris (world/box-tris 0.0 0.0 [1.0 2.0 0.5] 0.0)]
    (is (= 12 (count tris)))
    (is (every? #(= 3 (count %)) tris))))

(deftest box-tris-axis-aligned-extent
  (let [tris (world/box-tris 5.0 -3.0 [1.0 2.0 0.5] 0.0)
        pts (apply concat tris)
        xs (map first pts)
        ys (map second pts)
        zs (map #(nth % 2) pts)]
    (is (< (Math/abs (- 4.0 (apply min xs))) 1e-9))
    (is (< (Math/abs (- 6.0 (apply max xs))) 1e-9))
    (is (< (Math/abs (- -5.0 (apply min ys))) 1e-9))
    (is (< (Math/abs (- -1.0 (apply max ys))) 1e-9))
    (is (< (Math/abs (- 0.0 (apply min zs))) 1e-9))
    (is (< (Math/abs (- 1.0 (apply max zs))) 1e-9))))

(defn- scenario-1-actor []
  {:ego {:x 0.0 :y 0.0 :yaw 0.0 :speed 0.0 :extent [1.0 0.5 0.5]}
   :actors [{:id "a1" :kind "car" :x 10.0 :y 0.0 :yaw 0.0 :vx -5.0 :vy 0.0 :extent [1.0 1.0 1.0]}]
   :obstacles [{:id "o1" :kind "cone" :x 5.0 :y 3.0 :yaw 0.0 :extent [0.3 0.3 0.5]}]
   :ground-half-size 50.0})

(deftest build-sensor-scene-has-geometry
  (let [scene (world/build-sensor-scene (scenario-1-actor))]
    (is (pos? (:node-count (:bvh scene))))
    (is (pos? (:count (:soup scene))))))

(deftest world-aabb-axis-aligned
  (is (= {:min [-1.0 -2.0 0.0] :max [1.0 2.0 1.0]}
         (world/world-aabb 0.0 0.0 [1.0 2.0 0.5] 0.0))))

(deftest world-aabb-rotated-90-swaps-footprint
  (let [aabb (world/world-aabb 0.0 0.0 [1.0 2.0 0.5] (/ Math/PI 2.0))]
    (is (< (Math/abs (- -2.0 ((:min aabb) 0))) 1e-9))
    (is (< (Math/abs (- -1.0 ((:min aabb) 1))) 1e-9))
    (is (< (Math/abs (- 2.0 ((:max aabb) 0))) 1e-9))
    (is (< (Math/abs (- 1.0 ((:max aabb) 1))) 1e-9))))

(deftest ground-truth-combines-actors-and-obstacles
  (let [gt (world/ground-truth (scenario-1-actor))]
    (is (= 2 (count gt)))
    (is (= #{"a1" "o1"} (set (map :id gt))))
    (let [a (first (filter #(= "a1" (:id %)) gt))]
      (is (= "car" (:kind a)))
      (is (= [10.0 0.0 1.0] (:center a))))))
